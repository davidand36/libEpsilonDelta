#
# SConscript
# Copyright (C) 2007 David M. Anderson
#
# SCons "makefile" for TestGraphics2DLib.
#

Import( 'env' )
Import( 'platform' )
Import( 'compiler' )
Import( 'release' )
Import( 'useSTLport' )
if useSTLport:
    Import( 'stlportIncludeDir' )
Import( 'useBoost' )
if useBoost:
    Import( 'boostIncludeDir' )
Import( 'useLoki' )
if useLoki:
    Import( 'lokiIncludeDir' )
Import( 'useSDL' )
if useSDL:
    Import( 'sdlIncludeDir' )
    Import( 'sdlLibDir' )

includePath = [ '.', '#graphics2d', '#mmbase', '#math', '#util' ]
if useSTLport:
    includePath.append( stlportIncludeDir )
if useBoost:
    includePath.append( boostIncludeDir )
if useLoki:
    includePath.append( lokiIncludeDir )
if useSDL:
    includePath.append( sdlIncludeDir )

libPath = [ '#' ]
libs = [ 'EpsilonDelta_graphics2d', 'EpsilonDelta_mmbase', 'EpsilonDelta_util' ]
if useSTLport:
    if ( compiler == 'gcc' ):
        libs.append( 'stlport_gcc' )
if useLoki:
    libs.append( 'Loki' )
if useSDL:
    libPath.append( sdlLibDir );
    libs.append( 'SDL' )
    if ( platform == 'win32' ):
        libs.append( 'SDLmain' )
    libs.append( 'SDL_image' )

sources = [ 
            'TestGraphics2DLib.cpp'
          ]

progs = []
prog = env.Program( 'TestGraphics2DLib', sources, CPPPATH = includePath,
                    LIBS = libs, LIBPATH = libPath )
progs = progs + prog;

Return( 'progs' )
